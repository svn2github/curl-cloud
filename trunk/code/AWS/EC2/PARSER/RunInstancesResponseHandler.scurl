
{curl-file-attributes character-encoding = "utf8"}

|| Handler to parse a RunInstances response
{define-class library RunInstancesResponseHandler {inherits EC2ResponseHandler}
  field library-get private-set run-instances-response:RunInstancesResponse
  field private data:StringBuf
  field private tag-stack:{Array-of String}
  
  {constructor library {default}
    set self.run-instances-response = {RunInstancesResponse}
    set self.data = {StringBuf}
    set self.tag-stack = {{Array-of String}}
    {construct-super}
  }
  
  {method public {start-element
                     uri:String,
                     name:String,
                     qname:String,
                     atts:Attributes
                 }:void
    {self.data.clear}
    
    {switch name
     case "item" do
        {switch {self.tag-stack.top-of-stack}
         case "groupSet" do
            {self.run-instances-response.group-set.append {Group}}
         case "instancesSet" do
            {self.run-instances-response.instances-set.append {RunningInstance}}
         case "productCodes" do
            {self.run-instances-response.instances-tail.product-codes.append {ProductCode}}
         case "blockDeviceMapping" do
            {self.run-instances-response.instances-tail.block-device-mapping.append {InstanceBlockDeviceMapping}}
         else
            || do nothing
        }
    }
    {self.tag-stack.push name}
  }
  
  {method public {characters
                     ch:StringBuf,
                     start:int,
                     length:int
                 }:void
    {self.data.clear}
    {self.data.concat {ch.substr start, length}}
  }
  
  {method public {end-element
                     uri:String,
                     name:String,
                     qname:String
                 }:void
    {self.tag-stack.pop}
    
    {if {self.tag-stack.find "instancesSet"} >= 0 then
        {parse-running-instance
            self.run-instances-response.instances-tail,
            self.tag-stack,
            self.data,
            name
        }
     else
        {switch name
         case "requestId" do
            set self.run-instances-response.request-id = {self.data.to-String}
         case "reservationId" do
            set self.run-instances-response.reservation-id = {self.data.to-String}
         case "ownerId" do
            set self.run-instances-response.owner-id = {self.data.to-String}
         case "groupId" do
            set self.run-instances-response.group-tail.group-id = {self.data.to-String}
        }
    }
  }
}