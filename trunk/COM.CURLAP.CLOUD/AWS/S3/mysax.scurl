{curl-file-attributes character-encoding = "shift-jis"}

{import * from CURL.XML.SAX.PARSER}
{import * from CURL.GUI.STANDARD}
{define-class public MySAXApp {inherits DefaultHandler}
  
  field private output:VBox
  field private nesting-depth:int
  
  {constructor public {default parse-output:VBox}
    set self.output = parse-output
    {construct-super}
  }
  
  {method public {start-document}:void
    set self.nesting-depth = 0
    {self.output.add
        {text color="purple", Start document}
    }
  }
  
  {method public {end-document}:void
    {self.output.add
        {text color="purple", End document}
    }
  }
  
  {method public {start-element
                     uri:String,
                     name:String,
                     qname:String,
                     atts:Attributes
                 }:void
    set self.nesting-depth = self.nesting-depth + 1
    {self.output.add
        {HBox
            {Fill width = self.nesting-depth * 0.2in},
            {text color="navy", < },
            {text color="fuchsia", {value name}},
            {text color="navy", >}
        }
    }
  }
  
  {method public {end-element
                     uri:String,
                     name:String,
                     qname:String
                 }:void
    {self.output.add
        {HBox
            {Fill width = self.nesting-depth * 0.2in},
            {text color="navy", </ },
            {text color="fuchsia", {value name}},
            {text color="navy", > }
        }
    }
    set self.nesting-depth = self.nesting-depth - 1
  }
  
  {method public {characters
                     ch:StringBuf,
                     start:int,
                     length:int
                 }:void
    {self.output.add
        {HBox
            {Fill width = self.nesting-depth * 0.3in},
            {text {ch.substr start, length}}
        }
    }
  }
  
  {method public {error exception:SAXParseException}:void
    {throw exception}
  }
  
  {method public {fatal-error exception:SAXParseException}:void
    {throw exception}
  }
  
  {method public {warning exception:SAXParseException}:void
    {throw exception}
  }
  
}

{define-proc {error-message parse-exception:SAXParseException}:String
    let sb:StringBuf={StringBuf "Error detected"}
    {if {parse-exception.get-line-number} != 0
     then
        {sb.concat " at line " & {parse-exception.get-line-number}}
        {if {parse-exception.get-system-id} != null
         then
            {sb.concat " of " & {parse-exception.get-system-id}}
        }
    }
    {sb.concat ": " & {parse-exception.get-message}}
    {return {sb.to-String}}
}

