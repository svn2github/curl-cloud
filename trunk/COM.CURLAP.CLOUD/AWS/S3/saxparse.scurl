{curl-file-attributes character-encoding = "shift-jis"}

{import * from CURL.XML.SAX.PARSER}

{let package xr:XMLReader = {SAXParser}}
{let public parse-output:VBox = {VBox}}

{define-proc public {parse-and-display
                        input-stream:#TextInputStream
                    }:void
    || clear the parse output of any previous xml file
    {parse-output.clear}
    
    {let handler:MySAXApp = {MySAXApp.default parse-output}}
    {xr.set-content-handler handler}
    {xr.set-error-handler handler}
    
    {try
        || Parse from what is found by opening input-name
        {xr.parse {InputSource character-stream = input-stream}}
        
     catch file-exception:IOException do
        || Handle any exception thrown due to the file read
        {parse-output.add
            {text color="red",
                A problem occurred while reading the file:
                {value file-exception}
            }
        }
     catch parse-exception:SAXParseException do
        || Handle any exception thrown while parsing the XML
        {parse-output.add
            {text color="red",
                A problem occurred parsing the XML file:
                {error-message parse-exception}
            }
        }
    }
}