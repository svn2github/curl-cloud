{curl-file-attributes character-encoding = "shift-jis"}
|| ----------------------------------------------
|| Object info
{define-class public ObjectInfo
  field public-get private-set key:String
  field public-get private-set last-modified:DateTime
  field public-get private-set etag:String
  field public-get private-set size:int
  field public-get private-set storage-class:String
  field public-get private-set owner:#Owner
  
  {constructor public {default
                          key:String,
                          last-modified:DateTime,
                          etag:String,
                          size:int,
                          storage-class:String,
                          owner:#Owner
                      }
    set self.key = key
    set self.last-modified = last-modified
    set self.etag = etag
    set self.size = size
    set self.storage-class = storage-class
    set self.owner = owner
  }
  
  {factory package {from-xml
                      key:String = "",
                      lastmodified:String = "",
                      etag:String = "",
                      size:String = "",
                      storage-class:String = "",
                      owner:#Owner = null,
                      ...
                  }:ObjectInfo
    {return {ObjectInfo
                key,
                {DateTime lastmodified},
                etag,
                {size.to-int},
                storage-class,
                owner
            }
    }
  }
  
  {method public {key-exist? key:String}:bool
    {if {self.key.find-string key} >= 0 then
        {return true}
     else
        {return false}
        
    }
  }
}

{define-class public ObjectsInfo
  field public-get private-set object-info:{Array-of ObjectInfo}
  field public-get private-set common-prefixes:{Array-of String}
  
  {constructor package {default object-info:{Array-of ObjectInfo}, common-prefixes:{Array-of String}}
    set self.object-info = object-info
    set self.common-prefixes = common-prefixes
  }
  
  {factory package {from-xml object-info:{Array-of ObjectInfo} = {new {Array-of ObjectInfo}}, common-prefixes:{Array-of String} = {new {Array-of String}}}:ObjectsInfo
    {return {ObjectsInfo object-info, common-prefixes}}
  }
}

|| ----------------------------------------------
|| Owner
{define-class public Owner
  field public-get private-set id:String
  field public-get private-set display-name:String
  
  {constructor package {default id:String, display-name:String}
    set self.id = id
    set self.display-name = display-name
  }
  
  {factory package {from-xml
                       id:String = "",
                       displayname:String = "",
                       ...
                   }:Owner
    {return {Owner id, displayname}}
  }
}